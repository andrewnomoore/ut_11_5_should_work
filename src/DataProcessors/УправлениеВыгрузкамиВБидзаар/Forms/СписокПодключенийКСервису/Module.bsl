

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	КоличествоПодключений = Справочники.УчетныеЗаписиЭлектронныхТорговыхПлощадок.КоличествоПодключений(
		ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.ВидыЭлектронныхТорговыхПлощадок.Бидзаар"));

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если КоличествоПодключений = 0 Тогда
		ОткрытьФорму("Обработка.УправлениеВыгрузкамиВБидзаар.Форма.НастройкаПодключения", , ВладелецФормы);
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ЗакрытьФормуСпискаПодключений" Тогда
		Закрыть();
	ИначеЕсли ИмяСобытия = "ОбновитьСписокПодключений" Тогда
		Элементы.СписокПодключений.Обновить();
	ИначеЕсли ИмяСобытия = "ВывестиСообщение" И ЗначениеЗаполнено(Параметр) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Параметр);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПодключенийПередНачаломИзменения(Элемент, Отказ)

	Отказ = Истина;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключить(Команда)

	ОткрытьФорму("Обработка.УправлениеВыгрузкамиВБидзаар.Форма.НастройкаПодключения", , ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНастройкиАвторизации(Команда)
	
	ТекущиеДанные = Элементы.СписокПодключений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Организация", ТекущиеДанные.Организация);
	ОткрытьФорму("Обработка.УправлениеВыгрузкамиВБидзаар.Форма.НастройкаПодключения", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНастройкиАвторизации(Команда)

	Оповещение = Новый ОписаниеОповещения("ОчиститьНастройкиЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Настройки авторизации будут удалены, что приведет к невозможности управления выгрузкой товаров текущей учетной записи.
			|Очистить настройки?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);	

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаКлиенте
Процедура ОчиститьНастройкиЗавершение(Результат, Параметры) Экспорт 

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные = Элементы.СписокПодключений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОчисткаУспешна = ОчиститьНастройкиНаСервере(ТекущиеДанные.Ссылка);

	Если ОчисткаУспешна Тогда
		Элементы.СписокПодключений.Обновить();
		Оповестить("УчетнаяЗаписьДеактивирована", ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОчиститьНастройкиНаСервере(УчетнаяЗаписьЭТП)

	ОчисткаУспешна = ИнтеграцияСЭлектроннымиТорговымиПлощадками.ОчиститьНастройкиУчетнойЗаписи(УчетнаяЗаписьЭТП);

	Возврат ОчисткаУспешна;

КонецФункции

#КонецОбласти